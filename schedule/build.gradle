buildscript {
	ext {
		quartzVersion = '2.3.0'
		springVersion = '5.1.8.RELEASE'
		logbackVersion = '1.2.3'
		jupiterVersion = '5.5.2'
		dubboVersion = '2.7.5'
		dubboCuratorVersion = '2.13.0'

		// internal projects available
		apiAvailable = file('../../api').exists()
		apiVersion = '0.2.1-SNAPSHOT'
	}
}

plugins {
	id 'java'
	id 'maven-publish'
}

group = 'org.angboot.schedule'
version = '0.2.1-SNAPSHOT'

sourceCompatibility = '1.8'

configurations {
	internalApi
	compile.extendsFrom(internalApi)
}

repositories {
	maven {
		url 'http://javafamily.club:8081/repository/maven-public/'
		content {
			// put internal and custom artifacts here that aren't available in public repositories
			includeGroup 'org.angboot'
		}
	}

	maven {
		url 'http://maven.aliyun.com/nexus/content/groups/public/'
	}

	jcenter()
	mavenCentral()
}

dependencies {
	// internal api
	if(apiAvailable) {
		internalApi project(':api:models')
		internalApi project(':api:common')
		internalApi project(':api:interfaces')
		internalApi project(':api:constants')
	}
	else {
		internalApi "org.angboot:models:${apiVersion}"
		internalApi "org.angboot:common:${apiVersion}"
		internalApi "org.angboot:interfaces:${apiVersion}"
		internalApi "org.angboot:constants:${apiVersion}"
	}

	// spring
	implementation "org.springframework:spring-core:${springVersion}"
	implementation "org.springframework:spring-beans:${springVersion}"
	implementation "org.springframework:spring-context:${springVersion}"
	implementation "org.springframework:spring-context-support:${springVersion}"

	// quartz
	implementation ("org.quartz-scheduler:quartz:$quartzVersion") {
		exclude(group: 'org.slf4j')
	}

	implementation "org.quartz-scheduler:quartz-jobs:$quartzVersion"

	// dubbo
	implementation "org.apache.dubbo:dubbo-spring-boot-starter:${dubboVersion}"
	implementation "org.apache.curator:curator-recipes:${dubboCuratorVersion}"

	// Log
	implementation "ch.qos.logback:logback-classic:${logbackVersion}"

	// test
	testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
	testImplementation "org.springframework:spring-test:${springVersion}"

}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId project.group
			artifactId project.name
			version project.version
			from components.java

			pom.withXml {
				def pomNode = asNode()
				pomNode.dependencies.each {
					it.parent().remove(it)
				}
			}

			// upload source code
			artifact sourceJar {
				classifier "sources"
			}
		}
	}

	repositories {
		maven {
			credentials {
				username 'admin'
				password 'dreamLi0416!@#'
			}

			def snapshotsUrl = 'http://javafamily.club:8081/repository/maven-snapshots/'
			def releasesUrl = 'http://javafamily.club:8081/repository/maven-releases/'
			url = project.version.endsWith('-SNAPSHOT') ? snapshotsUrl : releasesUrl
		}
	}
}

test {
	useJUnitPlatform()
}

wrapper {
	gradleVersion = '6.0.1'
}

task runSchedule() {
	group 'application'
	description 'Run Schedule Server'

	doLast {
		javaexec {
			classpath sourceSets.main.runtimeClasspath
			main = 'org.angboot.schedule.ScheduleApplication'
			jvmArgs = [
				'-Djava.rmi.server.hostname=127.0.0.1',
				'-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5019'
			]

			args = ['--stop-port', '8085', '--stop-key', 'stop']
		}
	}
}



